apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
android {
    compileSdkVersion 29
    buildToolsVersion "29.0.2"

    defaultConfig {
        applicationId "com.same.part.assistant"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 8
        versionName "1.0.7"
        multiDexEnabled true
    }

    signingConfigs {
        debug {
            storeFile file(storeFile_)
            storePassword storePassword_
            keyAlias keyAlias_
            keyPassword keyPassword_
        }
        release {
            storeFile file(storeFile_)
            storePassword storePassword_
            keyAlias keyAlias_
            keyPassword keyPassword_
        }
    }

    buildTypes {
        debug{
            buildConfigField "Boolean","IS_TEST_URL",isTestUrlInDebug
        }
        release {
            buildConfigField "Boolean","IS_TEST_URL",isTestUrlInRelease
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    //数据绑定库
    dataBinding {
        enabled = true
    }

    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }

    dexOptions {
        javaMaxHeapSize "4g"
        jumboMode = true
        preDexLibraries = false
        additionalParameters = [
                '--multi-dex',//多分包
                '--set-max-idx-number=60000'//每个包内方法数上限
        ]
    }

    //设置apk打包名称，注意引用
    android.applicationVariants.all {
        variant ->
            variant.outputs.all {
                output ->
                    def outputFile = output.outputFile
                    if (outputFile.name.contains("debug")){
                        outputFileName = new File("../debug/", "${defaultConfig.applicationId}-${urlEnvironment(Boolean.parseBoolean(isTestUrlInDebug))}-${defaultConfig.versionCode}-${defaultConfig.versionName}-${releaseTime()}.apk")
                    }
                    if (outputFile.name.contains("release")){
                        outputFileName = new File("../release/", "${defaultConfig.applicationId}-${urlEnvironment(Boolean.parseBoolean(isTestUrlInRelease))}-${defaultConfig.versionCode}-${defaultConfig.versionName}-${releaseTime()}.apk")
                    }
            }
    }
}


def urlEnvironment(boolean isTestUrl){
    if (isTestUrl){
        return "test"
    }else {
        return "product"
    }
}

//打包的文件名引用系统时间
def releaseTime() {
    return new Date().format("yyyy-MM-dd HH-mm-ss", TimeZone.getTimeZone("Asia/Shanghai"))
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar','*.aar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.core:core-ktx:1.2.0'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation "com.google.android.material:material:1.1.0"
    implementation 'de.hdodenhof:circleimageview:3.1.0'
    implementation 'com.github.bumptech.glide:glide:4.11.0'
    implementation 'com.scwang.smartrefresh:SmartRefreshLayout:1.1.2'
    implementation 'com.github.pinguo-zhouwei:CustomPopwindow:2.1.1'
    implementation 'pub.devrel:easypermissions:3.0.0'
    implementation project(path: ':matisse')
    //项目核心框架JetpackMvvm
    implementation project(path: ':JetpackMvvm')
    //fastjson
    implementation 'com.alibaba:fastjson:1.1.71.android'
    //dialog
    implementation "com.afollestad.material-dialogs:lifecycle:3.3.0"
    implementation "com.afollestad.material-dialogs:core:3.3.0"
    implementation "com.afollestad.material-dialogs:color:3.3.0"
    implementation "com.afollestad.material-dialogs:datetime:3.3.0"
    implementation "com.afollestad.material-dialogs:bottomsheets:3.3.0"
    //util
    implementation 'com.blankj:utilcodex:1.26.0'
    //微信开源项目，替代SP
    implementation 'com.tencent:mmkv:1.0.22'
    //
    implementation 'com.android.support:multidex:1.0.3'
    //开关
    implementation 'com.github.zcweng:switch-button:0.0.3@aar'
    //七牛云
    implementation 'com.qiniu:qiniu-android-sdk:7.6.1'
    //网络请求框架
    implementation("com.squareup.okhttp3:okhttp:4.7.2")
    //底部弹框
    implementation 'me.shaohui:bottomdialog:1.1.9'
    //eventbus
    implementation 'org.greenrobot:eventbus:3.2.0'
    //二维码扫描
    implementation 'com.github.yuzhiqiang1993:zxing:2.2.5'
    //BaseAdapter
    implementation 'com.github.CymChad:BaseRecyclerViewAdapterHelper:3.0.2'
    //版本升级
    implementation 'com.github.AlexLiuSheng:CheckVersionLib:2.2.4'
}
